buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath files('libs/gradle-witness.jar')
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
        classpath "com.google.gms:google-services:$googleServicesVersion"
        classpath "com.squareup:javapoet:1.13.0"
        if (project.hasProperty('huawei')) classpath 'com.huawei.agconnect:agcp:1.9.1.300'
    }
}

// List plugins AND their versions here, but don't apply. This allows you to use the plugin
// in your module without specifying the version.
plugins {
    id 'com.google.devtools.ksp' version "$kotlinVersion-1.0.20" apply false
    id 'com.google.dagger.hilt.android' version "$daggerHiltVersion" apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()

        maven {
            url "https://raw.github.com/signalapp/maven/master/shortcutbadger/releases/"
            content {
                includeGroupByRegex "me\\.leolin.*"
            }
        }
        maven {
            url "https://raw.github.com/signalapp/maven/master/circular-progress-button/releases/"
            content {
                includeGroupByRegex "com\\.github\\.dmytrodanylyk\\.circular-progress-button\\.*"
            }
        }
        maven {
            url "https://raw.github.com/signalapp/maven/master/sqlcipher/release/"
            content {
                includeGroupByRegex "org\\.signal.*"
            }
        }
        maven { url "https://jitpack.io" }
    }

    project.ext {
        androidMinimumSdkVersion = 35
        androidTargetSdkVersion  = 35
        androidCompileSdkVersion = 35
    }
}
